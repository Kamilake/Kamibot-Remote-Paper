plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'io.papermc.paperweight.userdev' version '2.0.0-beta.14'
}

ext {
    releaseVersion = project.hasProperty('releaseVersion') ? project.releaseVersion : '0.0.1'
    minecraftVersion = project.hasProperty('minecraftVersion') ? project.minecraftVersion : '1.20'
}

repositories {
    mavenCentral()
    maven { name = "papermc"; url = uri("https://repo.papermc.io/repository/maven-public/") }
    maven { name = "jitpack"; url = uri("https://jitpack.io") }
    maven { name = "spigotmc"; url = uri("https://hub.spigotmc.org/nexus/content/repositories/snapshots/") }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.8.2'
    implementation 'com.google.guava:guava:31.0.1-jre'
    compileOnly("io.papermc.paper:paper-api:${minecraftVersion}-R0.1-SNAPSHOT")
    paperweight.paperDevBundle("${minecraftVersion}-R0.1-SNAPSHOT")
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    implementation 'org.java-websocket:Java-WebSocket:1.5.6'
    implementation 'org.json:json:20210307'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'kamibot.App'
}

tasks.named('test') {
    useJUnitPlatform()
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
    }
}

tasks.jar {
    manifest {
        attributes["paperweight-mappings-namespace"] = "mojang"
    }
}

tasks.processResources {
    filesMatching('plugin.yml') {
        expand(version: "${releaseVersion}")
    }
}

tasks.shadowJar {
    dependsOn tasks.processResources
    manifest {
        attributes["paperweight-mappings-namespace"] = "mojang"
    }
    archiveFileName = "Kamibot-Remote-Paper_${minecraftVersion}_${releaseVersion}.jar"
}

paperweight {
    reobfArtifactConfiguration = io.papermc.paperweight.userdev.ReobfArtifactConfiguration.getMOJANG_PRODUCTION()
}